pkgbase=mycroft-mimic1
_gh_org=MycroftAI
_gh_proj=mimic1
pkgname=("mycroft-mimic1" "mycroft-mimic1-voices")
_commit='adf655da0399530ac1b586590257847eb61be232'
pkgver='1.3.0.1'
pkgrel=02
arch=('x86_64' 'aarch64' 'armv7h')
url="https://github.com/MycroftAI/$_gh_proj"
license=('see https://github.com/MycroftAI/mimic1/blob/master/COPYING')
depends=()
makedepends=(gcc make pkg-config automake libtool)

source=("https://github.com/$_gh_org/$_gh_proj/archive/$_commit.tar.gz")
sha256sums=('c17b8d56c928df97d6ae0f3a4b1e4f6b60d939d25718fed43d29ec0f82f36d59')

build() {
    cd "$srcdir/$_gh_proj-$_commit"
    ./autogen.sh
    ./configure --enable-shared --prefix="/usr" CFLAGS="-Wno-free-nonheap-object" #--with-audio=alsa
    make
}

package_mycroft-mimic1() {
    depends=(alsa-lib)
    pkgdesc='Mimic is a fast, lightweight Text-to-speech engine developed by Mycroft A.I. and VocaliD, based on Carnegie Mellon Universityâ€™s Flite (Festival-Lite) software. Mimic takes in text and reads it out loud to create a high quality voice.'

    cd "$srcdir/$_gh_proj-$_commit" 
    echo "Installing $pkgname to $pkgdir"
    make DESTDIR="$pkgdir" install

    if [ -d "$srcdir/$pkgname/docs" ]; then
        mkdir -p "$pkgdir/usr/share/doc/$pkgname"  # Create doc directory
        cp -r "$srcdir/$pkgname/docs" "$pkgdir/usr/share/doc/$pkgname" || true
        chmod 644 "$pkgdir/usr/share/doc/$pkgname"/*.txt  || true # Set read permissions for documentation files
    fi
    
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}


package_mycroft-mimic1-voices() {
    arch=('any')
    pkgdesc='Flite engine voices for Mimic1'

    cd "$srcdir/$_gh_proj-$_commit" 
    echo "Installing $pkgname to $pkgdir"

    mkdir -p "${pkgdir}/usr/share/mimic/voices/"
    install -D -m644 ./voices/*.flitevox "${pkgdir}/usr/share/mimic/voices/"
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
